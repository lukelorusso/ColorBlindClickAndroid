apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion androidCompileSdkVersion

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        applicationId androidApplicationId
        targetSdkVersion androidCompileSdkVersion
        minSdkVersion androidMinSdkVersion
        versionCode androidVersionCode
        versionName androidVersionName
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner packageAndroidJUnitRunner
    }

    sourceSets {
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        def ENABLE_ANALYTICS = "ENABLE_ANALYTICS"
        debug {
            applicationIdSuffix = ".debug"
            versionNameSuffix "-DEBUG"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            buildConfigField "boolean", ENABLE_ANALYTICS, 'Boolean.parseBoolean("false")'
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            buildConfigField "boolean", ENABLE_ANALYTICS, 'Boolean.parseBoolean("true")'
        }
        // For Android Test
        debugTest {
            initWith(debug)
            minifyEnabled false
        }
        testBuildType 'debugTest'
    }

    lintOptions {
        disable 'ContentDescription'
    }
}

dependencies {
    // MODULE
    implementation project(':data')
    implementation project(':domain')

    // KOTLIN
    implementation kotlinStdlib

    // ANDROIDX
    implementation androidXAppCompat
    implementation androidXRecyclerView
    implementation androidXArch
    implementation androidXLifecycleViewModel
    implementation materialDesign
    implementation constraintLayout
    implementation multidex

    // RX
    implementation rxAndroid
    implementation rxKotlin
    implementation rxBinding
    implementation rxBindingSwipeRefreshLayout

    // ANDROID TEST
    androidTestImplementation androidXTestRunner
    androidTestImplementation androidXTestJunit
    androidTestImplementation espressoCore
    testImplementation junit

    // DATABASE
    implementation paperDB

    // CODE GENERATION
    implementation dagger
    kapt daggerCompiler

    // DEBUG TOOLS
    implementation timber
    implementation stetho

    // GSON
    implementation gson

    // UI
    implementation picasso
    implementation fotoapparat
    implementation verticalSeekBar
    implementation rateBottomSheet
    implementation hfRecyclerView

    // CRASHLYTICS
    implementation firebaseCore
    implementation(firebaseCrashlyticsNew) { transitive = true }
    implementation firebaseAnalytics

    // DEPENDENCY CHECK STRATEGY
    dependencyUpdates.resolutionStrategy dependencyUpdatesStrategy
}
