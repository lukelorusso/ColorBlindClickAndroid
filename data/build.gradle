apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion = androidCompileSdkVersion

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        minSdkVersion androidMinSdkVersion
        testInstrumentationRunner packageAndroidJUnitRunner
    }

    sourceSets {
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    buildTypes {
        debug {
            minifyEnabled true
            consumerProguardFiles dtoRules, gsonRules, modelRules, okhttp3Rules, retrofit2Rules
        }
        release {
            minifyEnabled true
            consumerProguardFiles dtoRules, gsonRules, modelRules, okhttp3Rules, retrofit2Rules
        }
        // For Android Test
        debugTest {
            initWith(debug)
            minifyEnabled false
        }
        testBuildType 'debugTest'
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_18
        targetCompatibility = JavaVersion.VERSION_18
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_18
    }

    namespace 'com.lukelorusso.data'
}

dependencies {
    // MODULE
    implementation project(':domain')

    // KOTLIN
    implementation kotlinStdlib

    // SUPPORT
    implementation androidXCore
    implementation androidXPreference

    // DEPENDENCY INJECTION
    implementation koinCore

    // WEBSERVICES
    implementation retrofitConverterGson
    implementation retrofitConverterScalars

    // DATABASE
    implementation paperDB

    // DEBUG TOOLS
    implementation timber
    debugImplementation chuckerDebug
    debugTestImplementation chuckerDebug
    releaseImplementation chuckerRelease
}
